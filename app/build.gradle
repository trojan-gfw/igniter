apply plugin: 'com.android.application'

ext.versionMajor = 0
ext.versionMinor = 10
ext.versionPatch = 1
ext.versionClassifier = "beta" // or null
ext.isSnapshot = false // set to false when publishing new releases
ext.minimumSdkVersion = 23
ext.targetSdkVersion = 30

private Integer GenerateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String GenerateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    if (ext.isSnapshot) {
        versionName += "-" + "SNAPSHOT"
    }
    return versionName;
}

android {
    ndkVersion "21.3.6528147"
    compileSdkVersion project.ext.targetSdkVersion

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    defaultConfig {
        applicationId "io.github.trojan_gfw.igniter"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode GenerateVersionCode()
        versionName GenerateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_CPP_FEATURES=rtti exceptions"
            }
        }
        archivesBaseName = "$applicationId-v$versionName-$versionCode"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        doNotStrip "*/armeabi-v7a/libgojni.so"
        doNotStrip "*/arm64-v8a/libgojni.so"
        doNotStrip "*/x86/libgojni.so"
        doNotStrip "*/x86_64/libgojni.so"
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }
}

repositories {
    flatDir {
        dirs 'src/libs'
    }
}

dependencies {
    implementation 'com.github.stealthcopter:AndroidNetworkTools:0.4.5.3'
    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'androidx.activity:activity:1.2.0-beta01'
    implementation 'androidx.fragment:fragment:1.3.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha06'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.core:core:1.5.0-alpha04'
    implementation 'androidx.preference:preference:1.1.1'
    def camerax_version = "1.0.0-beta11"
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    // CameraX View class
    implementation 'androidx.camera:camera-view:1.0.0-alpha18'
    implementation 'com.google.mlkit:barcode-scanning:16.0.3'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    api(name: 'golibs', ext: 'aar')
}
